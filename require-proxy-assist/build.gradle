buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'

// For publishing.
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile project(':require-proxy-runtime')
    compile 'com.github.stephanenicolas.morpheus:morpheus-plugin:1.0.7'
    compile gradleApi()
    compile localGroovy()
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = localProperties.getProperty('bintrayUser')
        key = localProperties.getProperty('bintrayApiKey')
        configurations = ['archives']
        publish = true
        pkg {
            repo = PROJECT_REPO
            name = 'require-proxy-assist'
            userOrg = user
            licenses = ['Apache-2.0']
            vcsUrl = PROJECT_VCS_URL
            version {
                name = PROJECT_VERSION
                desc = PROJECT_DESCRIPTION
            }
        }
    }
}
